import 'utilities.eol';

/*
mape(v.att) =
  SELECT
    c.att as res,
    TEMP_obj.val as val,
    TEMP_obj.ref_v as ref_v, for each v in SVars(v)
  FROM (mape(v)) as TEMP_obj
  LEFT JOIN c
  ON TEMP_obj.ref_v = c.c_id AND TEMP_obj.val = 1
*/
rule Attribute
transform op:OCL!PropertyCallExp
to ps:SQL!PlainSelect {

  op.attributeName().println('Attribute: ');

  ps.selItems.add(selectItem('res', column(op.entityName(), op.attributeName())));
  ps.selItems.add(selectItem('val', column('TEMP_obj', 'val')));

  for (sourceVar in op.source.sourceVars()) {
    var refName = 'ref_' + sourceVar.name;
    ps.selItems.add(selectItem(refName, column('TEMP_obj', refName)));
  }

  ps.fromItem = subselect('TEMP_obj', op.source.equivalent());

  ps.joins.add(leftJoinOn(
    table(op.entityName()),
    andExpression(
      equalsExpression(
        column('TEMP_obj', 'ref_' + op.variableName()),
        column(op.entityName(), op.entityName() + '_id')
      ),
      equalsExpression(
        column('TEMP_obj', 'val'),
        longValue(1l)
      )
    )
  ));
}
